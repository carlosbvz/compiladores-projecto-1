para aaa
;
; Este ejemplo le da una idea de cómo es el espacio 3D.
;
; También observe que el objeto no está en movimiento. Lo
; que ve en el espacio 3D es que se está moviendo.
;
perspectiva
borrapantalla
ocultatortuga
; "Vuele" el OJO de la tortuga en ARC sobre el objeto y el eje
activa -1
; Ahora vuele a una altitud de 600 pies
; *NO* borre los cinturones de seguridad, *TODOS* los vuelos son
; para no fumadores
pony 600
activa 0
; Continua volando
repite 72 ~
[
; Ahora vuele sobre un arco suave alrededor del objeto
; Siempre mirando la tortuga -2 (que continua en casa por defecto)
activa -1
; Fuera de nivel para ARC (seguimos apuntando al eje +Y)
giraizquierda 90
balancea 90
arc2 5 1000
; Regresa al punto +Y del eje porque la tortuga -1 también tiene efecto
; cuyo fin es "CABECEA"
balanceaizquierda 90
giraderecha 90
activa 0
; Limpiar la pantalla no reiniciará las tortugas -1 o -2 (sin embargo
; la perspectiva continuará)
borrapantalla
; Dibuja los 2 objetos siempre en el mismo lugar
cubo
ejes
espera 10
]
fin

para cubo
; Dibuja un muro, escala por él, se agacha y dibuja otro...
repite 4 [repite 4 [avanza 100 giraderecha 90] avanza 100 bajanariz 90]
fin

para ejes
;
; Aún cuando las "etiquetas" no puedan ser balanceadas o cabeceadas
; continuarán situándose en las coordenadas correctas. Pero el rumbo
; de la tortuga continua utilizándose en etiquetas y se ve afectado
; por balanceos y cabeceos.
;
avanza 200
rotula "+Y
retrocede 400
rotula "-Y
avanza 200
giraderecha 90
avanza 200
rotula "+X
retrocede 400
rotula "-X
avanza 200
giraizquierda 90
bajanariz 90
avanza 200
rotula "-Z
retrocede 400
rotula "+Z
avanza 200
cabecea 90
fin

para zzz
sisino sinobox [Bienvenido] [Selecciona SI para ver el ejemplo, Selecciona NO para ver el código.] [aaa] [editatodo]
fin

Haz "arranque [zzz]
